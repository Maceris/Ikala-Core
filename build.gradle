plugins {
    id 'application'
    id 'eclipse'
    id 'idea'
    id 'io.franzbecker.gradle-lombok' version '5.0.0'
    id 'com.diffplug.spotless' version '8.0.0'
}

import io.franzbecker.gradle.lombok.task.DelombokTask

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
    withSourcesJar()
    withJavadocJar()
}

base {
	archivesName = "${pluginName}"
}
version = "${pluginVersion}"

repositories {
    mavenCentral()
}

eclipse {
    classpath {
        downloadJavadoc = true
        downloadSources = true
    }
}

idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}

dependencies {
	implementation "com.google.code.gson:gson:${gsonVersion}"
	implementation "com.github.zafarkhaja:java-semver:${jsemverVersion}"
	implementation "org.apache.logging.log4j:log4j-api:${log4jAPIVersion}"
	implementation "org.apache.logging.log4j:log4j-core:${log4jCoreVersion}"
	implementation "org.apache.logging.log4j:log4j-slf4j2-impl:${log4jSlf4jVersion}"
	implementation "org.luaj:luaj-jse:${luaJVersion}"
	implementation "org.slf4j:slf4j-api:${slf4jVersion}"
	implementation "org.yaml:snakeyaml:${snakeYamlVersion}"
	implementation "org.antlr:antlr4-runtime:${antlrVersion}"
	
	// Testing
	testImplementation "org.awaitility:awaitility:${awaitilityVersion}"
	testCompileOnly "org.hamcrest:hamcrest:${hamcrestVersion}"
	testImplementation "org.junit.jupiter:junit-jupiter:${junitVersion}"
	testRuntimeOnly "org.junit.platform:junit-platform-launcher"
	testImplementation "org.mockito:mockito-core:${mockitoVersion}"
	testImplementation "org.mockito:mockito-junit-jupiter:${mockitoVersion}"
	
	// lombok
	compileOnly "org.projectlombok:lombok:${lombokVersion}"
	annotationProcessor "org.projectlombok:lombok:${lombokVersion}"
	testCompileOnly "org.projectlombok:lombok:${lombokVersion}"
	testAnnotationProcessor "org.projectlombok:lombok:${lombokVersion}"
}

tasks.register('delombok', DelombokTask) {
	description = 'Delomboks the source code'
    ext.outputDir = file("${delombokedSourceDir}")
    outputs.dir(outputDir)
    sourceSets.main.java.srcDirs.each {
        inputs.dir(it)
        args(it, "-d", outputDir)
    }
    doFirst {
        outputDir.deleteDir()
    }
}

tasks.named('test') {
	useJUnitPlatform()
	testLogging {
		exceptionFormat = 'full'
		events = ['passed', 'skipped', 'failed']
		showStandardStreams = true
	}
}

tasks.named('javadoc') {
	dependsOn = delombok as Iterable<?>
	source = delombok.outputDir
	failOnError = false
}

tasks.withType(Javadoc).configureEach {
	options.addStringOption('Xdoclint:-missing', '-quiet')
}

tasks.named('clean') {
	delete delombokedSourceDir
	// tests generate this
	delete 'logs'
}

application {
	mainClass = 'com.ikalagaming.launcher.Launcher'
}

tasks.named('jar') {
    from {
        [
        configurations.compileClasspath.collect {
        	it.isDirectory() ? it : zipTree(it).matching { exclude 'LICENSE' }
        },
        'docs/licenses/',
        'LICENSE'
        ]
    }
    exclude 'META-INF/*'
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

tasks.named('check') {
	dependsOn spotlessApply
}

spotless {
	java {
		target fileTree('.') {
			include '**/*.java'
			exclude '**/build/**', '**/build-*/**'
		}
		cleanthat().sourceCompatibility('17')
			.addMutator('SafeAndConsensual')
			.addMutator('SafeButNotConsensual')
			.addMutator('SafeButControversial')
			.excludeMutator('AvoidInlineConditionals')
			.includeDraft(false)
		googleJavaFormat('1.21.0').aosp()
		// eclipse('4.27').configFile('formatting.xml')
		toggleOffOn()
		removeUnusedImports()
		trimTrailingWhitespace()
		endWithNewline()
		importOrder('\\#', 'com.ikalagaming', '', 'java|javax')
		formatAnnotations()
	}
}
